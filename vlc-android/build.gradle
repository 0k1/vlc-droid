apply plugin: 'com.android.application'


task buildDebug(type:Exec) {
    /*
    Properties set for Android Studio own shell.
    when you run gradlew from cli, OS shell env variables will be used

    To be able to build from Android Studio, you have to set ndk.dir & sdk.dir
    properties in local.properties in the root folder, like this (for example):
    sdk.dir=/home/<username>/SDK/android-sdk-linux
    ndk.dir=/home/<username>/SDK/android-ndk-r10b
     */
    if (System.getenv('ANDROID_SDK') == null || System.getenv('ANDROID_NDK') == null) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        environment 'ANDROID_NDK', properties.getProperty('ndk.dir')
        environment 'ANDROID_SDK', properties.getProperty('sdk.dir')
        environment 'ANDROID_ABI', 'armeabi-v7a x86'
    }
    workingDir '..'
    commandLine './compile.sh'
    args('jni')
}
task buildRelease(type:Exec) {
    if (System.getenv('ANDROID_SDK') == null || System.getenv('ANDROID_NDK') == null) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        environment 'ANDROID_NDK', properties.getProperty('ndk.dir')
        environment 'ANDROID_SDK', properties.getProperty('sdk.dir')
        environment 'ANDROID_ABI', 'armeabi-v7a x86'
    }

    workingDir '..'
    commandLine './compile.sh'
    args('jni', 'release')
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    lintOptions {
	  abortOnError false
      disable 'MissingTranslation', 'ExtraTranslation'
    }

    defaultConfig {
        applicationId "org.videolan.vlc"
        minSdkVersion 7
        targetSdkVersion 21
    }

     signingConfigs {

        release {
            /*
            To set this properties, create file gradle.properties with these 3 props.
            e.g.
            keyStoreFile=/home/<username>/.android/debug.keystore
            storealias=androiddebugkey
            storepwd=android
             */
            storeFile file(keyStoreFile)
            storePassword storepwd
            keyAlias storealias
            keyPassword storepwd
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            tasks.assembleRelease.dependsOn buildRelease
        }
        debug {
            tasks.assembleDebug.dependsOn buildDebug
        }
    }
    productFlavors {
        vanilla {
            tasks.whenTaskAdded { task ->
                if (task.name == 'assembleVanillaDebug') {
                    task.dependsOn buildDebug
                } else if (task.name == 'assembleVanillaRelease') {
                    task.dependsOn buildRelease
                }
            }
            applicationId "org.videolan.vlc"
        }
        tv {
            tasks.whenTaskAdded { task ->
                if (task.name == 'assembleTvDebug') {
                    task.dependsOn buildDebug
                } else if (task.name == 'assembleTvRelease') {
                    task.dependsOn buildRelease
                }
            }
            applicationId "org.videolan.vlc.tv"
            minSdkVersion 17
        }
    }

    sourceSets.main {
        manifest.srcFile 'AndroidManifest.xml'
        java.srcDirs = ['src']
        resources.srcDirs = ['src']
        aidl.srcDirs = ['src']
        renderscript.srcDirs = ['src']
        res.srcDirs = ['res']
        assets.srcDirs = ['assets']
    }
    sourceSets.tv {
        manifest.srcFile 'tv/AndroidManifest.xml'
        java.srcDirs = ['tv/src']
        resources.srcDirs = ['tv/src']
        aidl.srcDirs = ['tv/src']
        renderscript.srcDirs = ['tv/src']
        res.srcDirs = ['tv/res']
        assets.srcDirs = ['tv/assets']
    }
}

dependencies {
    compile project(':WheelView')
    compile project(':libvlc')
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.android.support:cardview-v7:21.0.0'
    tvCompile 'com.android.support:leanback-v17:21.0.0'
}