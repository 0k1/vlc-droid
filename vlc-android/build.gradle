apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    lintOptions {
        abortOnError false
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    defaultConfig {
        applicationId "org.videolan.vlc"
        minSdkVersion 7
        targetSdkVersion 21

        //Set the build ABI according to build types
        tasks.whenTaskAdded { task ->
            if (task.name.startsWith('assemble')) {
                if (task.name.endsWith('Debug'))
                    task.dependsOn(":libvlc:buildDebugARM")
                else if (task.name.endsWith('Debugx86'))
                    task.dependsOn(":libvlc:buildDebugx86")
            }
        }
    }

    signingConfigs {

        release {
            /*
            To set this properties, create file gradle.properties with these 3 props.
            e.g.
            keyStoreFile=/home/<username>/.android/debug.keystore
            storealias=androiddebugkey
            storepwd=android
             */
            storeFile file(keyStoreFile)
            keyAlias storealias
            if (System.getenv('PASSWORD_KEYSTORE') != null){
                storePassword = System.getenv('PASSWORD_KEYSTORE')
                keyPassword = System.getenv('PASSWORD_KEYSTORE')
            } else {
                storePassword storepwd
                keyPassword storepwd
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFile 'proguard.cfg'
        }
        debug {
            applicationIdSuffix ".debug"
        }
        debugx86.initWith(debug)
    }
    productFlavors {
        vanilla {
            applicationId "org.videolan.vlc"
        }
        tv {
            applicationId "org.videolan.vlc"
            minSdkVersion 17
        }
    }

    sourceSets.main {
        manifest.srcFile 'AndroidManifest.xml'
        java.srcDirs = ['src']
        resources.srcDirs = ['src']
        aidl.srcDirs = ['src']
        renderscript.srcDirs = ['src']
        res.srcDirs = ['res']
        assets.srcDirs = ['assets']
    }
    sourceSets.tv {
        manifest.srcFile 'tv/AndroidManifest.xml'
        java.srcDirs = ['tv/src']
        resources.srcDirs = ['tv/src']
        aidl.srcDirs = ['tv/src']
        renderscript.srcDirs = ['tv/src']
        res.srcDirs = ['tv/res']
        assets.srcDirs = ['tv/assets']
    }
}

dependencies {
    compile project(':WheelView')
    compile project(':libvlc')
    compile 'com.android.support:appcompat-v7:21.0.+'
    compile 'com.android.support:cardview-v7:21.0.+'
    compile 'com.android.support:recyclerview-v7:21.0.+'
    tvCompile 'com.android.support:leanback-v17:21.0.+'
}
